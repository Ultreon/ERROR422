plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}
architectury {
    platformSetupLoomIde()
    forge()
}


loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
    runs {
        client {
            programArgs "-mixin.config=ultreonlib-common.mixins.json"
            programArgs "-mixin.config=advanced-debug-common.mixins.json"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:$minecraft_version-$forge_version"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${architectury_version}"

    modApi "com.github.Ultreon.ultreonlib:ultreon-lib-forge:$ultreonlib_version"
    modApi("com.github.Ultreon.advanced-debug:advanced-debug-forge:$advanced_debug_version") {
        exclude group: "curse.maven"
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-collections-v0:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-commons-v0:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-crash-v0:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-datetime-v0:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-events-v1:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-functions-v0:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-registries-v0:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-resources-v0:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-text-v0:$corelibs_version")
    forgeRuntimeLibrary implementation("com.github.Ultreon.corelibs:corelibs-translations-v0:$corelibs_version")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
    finalizedBy "obfJar"
}

jar {
    classifier "dev"
}

tasks.register('obfJar', Jar) {
    dependsOn remapJar
    classifier "obf"

    doLast {
        FileOutputStream output = new FileOutputStream(outputs.files.singleFile)
        output.write(new byte[]{0x50, 0x4B, 0x03, 0x04})
        output.flush()
        FileInputStream input = new FileInputStream("$projectDir/build/libs/${archiveBaseName.get()}-${version}.jar")
        def inJar = input.readAllBytes()
        output.write(inJar)
        output.flush()
        output.close()
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}